/* REXX */
x = time('r')
parse arg port

if port = '' then
   port = '9000' /* default port */


/* "alloc f(systcpd) da('SYS5.TCP.PARMLIB(TCPDAT10)') shr" */
/* Prevent message EZY1372W Dataset *.TCPIP.DATA not found */


tmp = socket('terminate','ShowRxSv')
say 'Terminate =' tmp
tmp = socket('Initialize','ShowRxSv')
say 'Initialize=' tmp

tmp = socket('Socket')
parse var tmp . s

tmp = Socket('GetHostId')
parse var tmp . ipaddress
say 'Local IP  :' ipaddress

tmp = socket('Bind',s,'AF_INET' port ipaddress)
say 'Bind      =' tmp

tmp = socket('Listen',s,10)
say 'Listen    =' tmp

timeout      = 60
sockevtlist = 'Write Read * Exception'
sellist = Socket('Select',sockevtlist,timeout)
parse upper var sellist . 'READ' orlist 'WRITE' owlist 'EXCEPTION' .
say sellist orlist owlist
If orlist = '' & owlist = '' then do
   /* No read or write requests = Timeout */
   say 'Timeout ('timeout 'seconds) occured, leaving'
   exit
end

tmp = socket('Accept',s)
parse var tmp . ns . np nia .
say 'ShowRxSv: Connected by' nia 'on port' np 'and socket' ns

buffer = ''
total_length = 0
count = 0
do forever
   tmp = socket('Recv',ns)        /* Receive data socket */
   parse var tmp rc length data
   if rc Â¬=0 then leave           /* Bad rc */
   if length = 0 then leave       /* End of data */
   total_length = total_length + length  /* remember total length */
   buffer = buffer || data        /* keep the whole data */
   count = count+1
end

tmp = socket('close',s)           /*  Close listener socket */
tmp = socket('terminate','ShowRxSv')

say 'ShowRxSv received ' total_length 'Bytes from ShowMvs running on',
    nia 'port' np
say 'Count=' count

start = 1                         /* process the data  */
do until start>total_length
   line = substr(buffer,start,125)
   start = start+125
   say substr(line,1,75)          /* Debug */
end

exit


